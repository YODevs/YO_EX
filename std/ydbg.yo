#[app::classname("dbg")]
#[cfg::null_safety(true)]

include 'ystdio'
include 'yolib'

private static let timer : init system.diagnostics.stopwatch()

func print(list : yolib.list) : void
{
  let count : i32 = list::count()
  for(index in [0..count])
  {
    let item : str = list::get(index)
    io::println(item)
  }
}

func print(map : yolib.map) : void
{
  let size : i32 = map::size()
  for(index in [0..size])
  {
    let key : str = ""
    let value : str = ""
    map::get_map(index,key,value)
    io::println("Key -> #{key}#nlValue -> #{value}")
  }
}

func print(arr[] : str)
{
    let len : i32 = arr::length
    for(index in [0..len]){
        let val : str = arr[index]
        io::println(val)
    }
}

func print(matrix : yolib.matrix) : void
{
  let result : str = matrix::get_matrix()
  io::println(result)
}

func is_i32(val:str) : bool
{
  let i : i32 = 0
  let result : bool = val::contains(" ")
  if(result == true)
  {
    return false
  }
  else
  {
    result := system.int32::tryparse(val,i)
    return result
  }
}

func is_i32(val:obj) : bool
{
  let valtostr : str = [str]val
  let result : bool = is_i32(valtostr)
  return result
}

func is_bool(val:str) : bool
{
  val := val::tolower()
  match(val)
  {
    case "true"
    {
      return true
    }
    case "false"
    {
      return true
    }
    default
    {
      return false
    }
  }
}

func is_bool(val:obj) : bool
{
  let valtostr : str = [str]val
  let result : bool = is_bool(valtostr)
  return result
}

func is_f32(val:str) : bool
{
  let result : bool = val::contains(" ")
  if(result == false)
  {
    result := val::contains(".")
    if(result == true)
    {
      let f : f32 = 0.0
      result := system.single::tryparse(val,f)
      return result
    }
    else
    {
      return false
    }
  }
  else
  {
    return false
  }
}

func is_f32(val:obj) : bool
{
  let valtostr : str = [str]val
  let result : bool = is_f32(valtostr)
  return result
}

func is_space(val:str) : bool
{
  let result : bool = false
  let length : i32 = val::length
  if(val::length >> 0)
  {
    for(index in [0..length])
    {
      result := system.char::iswhitespace(val,index)
      if(result == false)
      {
        return false
      }
    }
    return true
  }
  else
  {
    return false
  }
}

func is_space(val:obj) : bool
{
  let valtostr : str = [str]val
  let result : bool = is_space(valtostr)
  return result
}

func is_null(val : str , nullvalstr : bool) : bool
{
  if(nullvalstr == true)
  {
    val := val::tolower()
    if(val == "null")
    {
      return true
    }
    return false
  }
  if(val == null)
  {
    return true
  }
  return false
}

func is_null(val : obj , nullvalstr : bool) : bool
{
  let valtostr : str = [str]val
  let result : bool = false
  if(nullvalstr == true)
  {
    result := is_null(valtostr,true)
    return result
  }
  if(val == null)
  {
    return true
  }
  return false
}

func timer_start() : void
{
  timer::start()
}

func timer_stop() : void
{
  timer::stop()
}

func exec_time() : i64
{
  let result : i64 = timer::elapsedmilliseconds
  return result
}

func timer_status() : bool
{
  let status : bool = timer::isrunning
  return status
}
